buildscript {
	repositories {
		maven {url = 'https://maven.minecraftforge.net'}
		maven {url = 'https://repo.spongepowered.org/repository/maven-public/'}
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
		classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
	}
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

archivesBaseName = "${rootProject.name}-forge-${project.minecraftVersion}"

dependencies {
	minecraft "net.minecraftforge:forge:${project.forgeVersion}"
	annotationProcessor "org.spongepowered:mixin:0.8.4-SNAPSHOT:processor"
	compileOnly project(":Common")
	
	def botania = implementation "vazkii.botania:Botania:${project.botaniaVersion}-FORGE${project.botaniaSnapshotStr}"
	if (project.declareBotaniaAsChanging == "true" && botania instanceof ExternalModuleDependency) {
		((ExternalModuleDependency) botania).changing = true
	}
	
	//botania-forge's transitive dependencies as of Sep 16, 2022; https://github.com/artemisSystem/Botania/blob/c18a609fd90e17853a55fa822e15042b5470673d/Forge/build.gradle#L103-L116
	//Botania does not (currently) publish transitive dependency information for its artifacts.
	
	implementation "vazkii.patchouli:Patchouli:1.19.2-76"
	
	compileOnly "mezz.jei:jei-1.19.2-common-api:11.2.0.247"
	runtimeOnly "mezz.jei:jei-1.19.2-forge:11.2.0.247"
	compileOnly "top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0:api"
	runtimeOnly "top.theillusivec4.curios:curios-forge:1.19.2-5.1.1.0"
	// compileOnly "com.blamejared.crafttweaker:CraftTweaker-forge-1.18.2:9.1.123"
	compileOnly "com.unascribed:ears-api:1.4.5"
}

mixin {
	add sourceSets.main, "${project.modId}.refmap.json"
	
	config "${project.modId}.mixin.common.json"
	config "${project.modId}.mixin.forge.json"
}

minecraft {
	mappings channel: 'official', version: project.minecraftVersion
	
	runs {
		client {
			workingDirectory file('./run')
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			ideaModule "${rootProject.name}.${project.name}.main"
			mods {
				incorporeal3 {
					source sourceSets.main
					source project(":Common").sourceSets.main
				}
			}
		}
		
		server {
			workingDirectory file('./run')
			property 'forge.logging.console.level', 'debug'
			property 'fml.earlyprogresswindow', 'false'
			ideaModule "${rootProject.name}.${project.name}.main"
			mods {
				incorporeal3 {
					source sourceSets.main
					source project(":Common").sourceSets.main
				}
			}
		}
	}
}

//fold in xplat resources and sources
processResources {
	from project(":Common").sourceSets.main.resources
}

tasks.withType(JavaCompile) {
	source(project(":Common").sourceSets.main.allSource)
}