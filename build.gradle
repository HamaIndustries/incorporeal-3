subprojects {
	apply plugin: "java"
	java.toolchain.languageVersion = JavaLanguageVersion.of(17)
	java.withSourcesJar()
	
	tasks.withType(JavaCompile).configureEach {
		it.options.encoding = "UTF-8"
		it.options.release = 17
	}
	
	tasks.withType(GenerateModuleMetadata) {
		//This shit is broke
		enabled = false
	}
	
	processResources {
		inputs.property "version", project.version
		
		filesMatching(["**/fabric.mod.json", "**/mods.toml"]) {
			expand "version": project.version
		}
		
		//Random things that might get left around if I'm sloppy.
		exclude("**/*.bat", "**/*.psd", "**/*.exe", "**/*.kra")
		//Anything explicitly marked unused.
		exclude("**/unused")
		//Vanilla datagenerator .cache file.
		//NB: This also fixes processResources blowing up with DuplicatedHandlingStrategy stuff
		//when merging the loader-specific jar with the Common jars
		exclude(".cache")
	}
	
	dependencies {
		//This mf keeps disappearing on me
		implementation "org.jetbrains:annotations:16.0.2"
	}
	
	repositories {
		//Spongfrog
		maven {
			name = "spunchbob"
			url = "https://repo.spongepowered.org/repository/maven-public/"
			content {
				includeGroup "org.spongepowered"
			}
		}
		
		//botania-xplat, botania-fabric, patchouli, CraftTweaker
		maven {
			name = "Jared"
			url = "https://maven.blamejared.com/"
			content {
				includeGroup "vazkii.botania"
				includeGroup "vazkii.patchouli"
				includeGroup "com.blamejared.crafttweaker"
			}
		}
		
		//Fiber
		maven {
			name = "modmuss50"
			url = "https://maven.modmuss50.me"
			content {
				includeGroup "me.zeroeightsix"
			}
		}
		
		//Cardinal Components
		maven {
			name = "Ladysnake"
			url = "https://ladysnake.jfrog.io/artifactory/mods"
			content {
				includeGroup "io.github.onyxstudios.Cardinal-Components-API"
				includeGroup "dev.onyxstudios.cardinal-components-api" //i guess they moved it
			}
		}
		
		//Trinkets
		maven {
			name = "Terraformers"
			url = "https://maven.terraformersmc.com/"
			content {
				includeGroup "dev.emi"
				
				//At the moment, required to pick up Mod Menu
				// https://github.com/emilyploszaj/trinkets/issues/159
				includeGroup "com.terraformersmc"
			}
		}
		
		//Reach Entity Attributes
		maven {
			name = "jamieswhiteshirt"
			url = "https://maven.jamieswhiteshirt.com/libs-release/"
			content {
				includeGroup "com.jamieswhiteshirt"
			}
		}
		
		//Step Height Entity Attribute
		maven {
			name = "jitpack :("
			url = "https://jitpack.io"
			content {
				includeGroup "com.github.emilyploszaj"
			}
		}
		
		//Roughly Enough Items and friends (someone includes this transitively)
		maven {
			name = "shedaniel"
			url = "https://maven.shedaniel.me"
			content {
				includeGroup "me.shedaniel"
				includeGroup "me.shedaniel.cloth"
				includeGroup "dev.architectury"
			}
		}
		
		//JEI
		maven {
			name = "progwml6"
			url = "https://dvs1.progwml6.com/files/maven"
			content {
				includeGroup "mezz.jei"
			}
		}
		
		//Curios
		maven {
			name = "theillusivec4"
			url = "https://maven.theillusivec4.top/"
			content {
				includeGroup "top.theillusivec4.curios"
			}
		}
		
		//Ears
		maven {
			name = "Sleeping Town"
			url = "https://repo.sleeping.town"
			content {
				includeGroup "com.unascribed"
			}
		}
	}
}